#@TYPE: Machine
#@NAME: Freescale i.MX6SLL Evaluation Kit
#@SOC: i.MX6SLL
#@DESCRIPTION: Machine configuration for Freescale i.MX6SLL EVK
#@MAINTAINER: Lauren Post lauren.post@nxp.com

MACHINEOVERRIDES =. "mx6:mx6sll:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc

MACHINE_FIRMWARE_append = " firmware-imx-epdc firmware-imx-brcm"

KERNEL_DEVICETREE = "imx6sll-evk.dtb imx6sll-evk-btwifi.dtb"

UBOOT_CONFIG ??= "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'sd-optee', 'sd', d)}"
UBOOT_CONFIG[sd] = "mx6sllevk_config,sdcard"
UBOOT_CONFIG[epdc] = "mx6sllevk_epdc_config"
UBOOT_CONFIG[mfgtool] = "mx6sllevk_config"
UBOOT_CONFIG[sd-optee] = "mx6sllevk_optee_config,sdcard"

SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_FEATURES += "pci wifi bluetooth"
MACHINE_FEATURES_append_use-fsl-bsp = " optee"

# MESA DRI library
XSERVER += "mesa-driver-swrast"

PREFERRED_PROVIDER_u-boot = "u-boot-imx"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-imx"
