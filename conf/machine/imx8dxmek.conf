#@TYPE: Machine
#@NAME: i.MX 8DX MEK
#@DESCRIPTION: i.MX 8DX Multisensory Enablement Kit (MEK) board
#@MAINTAINER: Lauren Post <lauren.post@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8x:mx8qxp:mx8dx:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa35.inc

IMX_DEFAULT_BSP = "nxp"

MACHINE_FEATURES += "pci optee bcm43455 bcm4356"
MACHINE_FEATURES_append_use-nxp-bsp = " bcm4359"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyAMA0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/imx8dx-mek.dtb"
KERNEL_DEVICETREE_append_use-nxp-bsp = " \
    freescale/imx8dx-mek-dsi-rm67191.dtb \
    freescale/imx8dx-mek-dsi-rm67191-rpmsg.dtb \
    freescale/imx8dx-mek-dsp.dtb \
    freescale/imx8dx-mek-enet2-tja1100.dtb \
    freescale/imx8dx-mek-it6263-lvds0-dual-channel.dtb \
    freescale/imx8dx-mek-it6263-lvds0-dual-channel-rpmsg.dtb \
    freescale/imx8dx-mek-it6263-lvds1-dual-channel.dtb \
    freescale/imx8dx-mek-it6263-lvds1-dual-channel-rpmsg.dtb \
    freescale/imx8dx-mek-jdi-wuxga-lvds0-panel.dtb \
    freescale/imx8dx-mek-jdi-wuxga-lvds0-panel-rpmsg.dtb \
    freescale/imx8dx-mek-jdi-wuxga-lvds1-panel.dtb \
    freescale/imx8dx-mek-jdi-wuxga-lvds1-panel-rpmsg.dtb \
    freescale/imx8dx-mek-ov5640.dtb \
    freescale/imx8dx-mek-ov5640-rpmsg.dtb \
    freescale/imx8dx-mek-rpmsg.dtb \
"

UBOOT_MAKE_TARGET = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', '', \
                                                       'u-boot.bin', d)}"
SPL_BINARY = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'spl/u-boot-spl.bin', \
                                                       '', d)}"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx8dx_mek_config,sdcard"
UBOOT_CONFIG[fspi] = "imx8dx_mek_fspi_defconfig"

IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS_SD = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl', \
                                                       'flash', d)}"
IMXBOOT_TARGETS_FSPI = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl_flexspi', \
                                                       'flash_flexspi', d)}"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG',        'sd', '${IMXBOOT_TARGETS_SD}', \
                                                       '${IMXBOOT_TARGETS_FSPI}', d)}"

BOARD_TYPE = "mek"

# Add additional firmware
MACHINE_FIRMWARE_append = " linux-firmware-ath10k"
