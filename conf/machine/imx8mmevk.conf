#@TYPE: Machine
#@NAME: NXP i.MX8MM LPDDR4 Evaluation Kit and i.MX8MM DDR4 Evaluation Kit
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for NXP i.MX8MMini LPDDR4 EVK and i.MX8MMini DDR4 EVK
#@MAINTAINER: Jun Zhu <junzhu@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

MACHINE_FEATURES += "pci wifi bluetooth optee"

# NXP BSP can consume QCA9377 driver and firmware
# Since the firmware is not available publicly, and rather distributed
# under "Proprietary" license - we opt-out from using it in all BSPs
# and pin it to NXP BSP only
MACHINE_FEATURES_append_use-nxp-bsp = " qca9377"

# Mainline kernel contains only one DTB file for
# imx8mmevk machine
KERNEL_DEVICETREE = "freescale/imx8mm-evk.dtb"

# NXP kernel has additional DTB files for various board configuration and
# derivates. Include them here for NXP BSP only
KERNEL_DEVICETREE_append_use-nxp-bsp = " \
       freescale/imx8mm-ddr4-evk.dtb \
       freescale/imx8mm-ddr4-evk-revb.dtb \
       freescale/imx8mm-ddr4-evk-revb-rm67191.dtb \
       freescale/imx8mm-ddr4-evk-rm67191.dtb \
       freescale/imx8mm-evk-ak4497.dtb \
       freescale/imx8mm-evk-ak5558.dtb \
       freescale/imx8mm-evk-audio-tdm.dtb \
       freescale/imx8mm-evk-inmate.dtb \
       freescale/imx8mm-evk-revb.dtb \
       freescale/imx8mm-evk-rm67191.dtb \
       freescale/imx8mm-evk-root.dtb \
       freescale/imx8mm-evk-rpmsg.dtb \
"


UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "imx8mm_evk_config,sdcard"
UBOOT_CONFIG[fspi] = "imx8mm_evk_fspi_defconfig"
UBOOT_CONFIG[mfgtool] = "imx8mm_evk_config"
SPL_BINARY = "spl/u-boot-spl.bin"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
	lpddr4_pmu_train_1d_imem.bin \
	lpddr4_pmu_train_1d_dmem.bin \
	lpddr4_pmu_train_2d_imem.bin \
	lpddr4_pmu_train_2d_dmem.bin \
"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mm-evk.dtb"

# Set ATF platform name
ATF_PLATFORM = "imx8mm"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_evk_flexspi', 'flash_evk', d)}"
IMX_BOOT_SOC_TARGET = "iMX8MM"

# Set Serial console
SERIAL_CONSOLES = "115200;ttymxc1"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""
IMX_BOOT_SEEK = "33"

OPTEE_BIN_EXT = "8mm"

# Add additional firmware
MACHINE_FIRMWARE_append = " linux-firmware-ath10k"
