#@TYPE: Machine
#@NAME: NXP i.MX6ULZ 14x14 Evaluation Kit
#@SOC: i.MX6ULZ
#@DESCRIPTION: Machine configuration for NXP i.MX6ULZ 14x14 EVK
#@MAINTAINER: Jun Zhu <junzhu@nxp.com>

MACHINEOVERRIDES =. "mx6:mx6ul:mx6ull:mx6ulz:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa7.inc

MACHINE_FEATURES += "wifi bluetooth bcm4339 bcm43455"

KERNEL_DEVICETREE = " \
    imx6ulz-14x14-evk.dtb \
"
KERNEL_DEVICETREE:append:use-nxp-bsp = " \
    imx6ulz-14x14-evk-btwifi.dtb \
    imx6ulz-14x14-evk-emmc.dtb \
    imx6ulz-14x14-evk-gpmi-weim.dtb \
"

UBOOT_MAKE_TARGET = "u-boot.imx"
UBOOT_SUFFIX = "imx"

UBOOT_CONFIG ??= " \
    sd \
    ${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'sd-optee', '', d)} \
"
UBOOT_CONFIG[sd]       = "mx6ulz_14x14_evk_config,sdcard"
UBOOT_CONFIG[sd-optee] = "mx6ulz_14x14_evk_optee_config,sdcard"
UBOOT_CONFIG[emmc]     = "mx6ulz_14x14_evk_emmc_config,sdcard"
UBOOT_CONFIG[nand]     = "mx6ulz_14x14_evk_nand_config,ubifs"
UBOOT_CONFIG[qspi1]    = "mx6ulz_14x14_evk_qspi1_config"

OPTEE_BIN_EXT = "6ulzevk"
