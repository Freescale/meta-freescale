#@TYPE: Machine
#@NAME: NXP i.MX6SL Evaluation Kit
#@SOC: i.MX6SL
#@DESCRIPTION: Machine configuration for NXP i.MX6SL Evaluation Kit
#@MAINTAINER: Otavio Salvador <otavio@ossystems.com.br>

MACHINEOVERRIDES =. "mx6:mx6sl:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc

KERNEL_DEVICETREE = "imx6sl-evk.dtb"
KERNEL_DEVICETREE_append_use-nxp-bsp = " \
    imx6sl-evk-btwifi.dtb \
    imx6sl-evk-csi.dtb \
    imx6sl-evk-ldo.dtb \
    imx6sl-evk-uart.dtb \
"

UBOOT_MAKE_TARGET = "u-boot.imx"
UBOOT_SUFFIX = "imx"

UBOOT_CONFIG ??= " \
    sd \
    ${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'sd-optee', '', d)} \
"
UBOOT_CONFIG[sd]       = "mx6slevk_config,sdcard"
UBOOT_CONFIG[sd-optee] = "mx6slevk_optee_config,sdcard"
UBOOT_CONFIG[epdc]     = "mx6slevk_epdc_config"
UBOOT_CONFIG[spinor]   = "mx6slevk_spinor_config"
UBOOT_CONFIG[mfgtool]  = "mx6slevk_config"

OPTEE_BIN_EXT = "6slevk"

SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_FEATURES += "pci wifi bluetooth bcm4339 bcm43455"

MACHINE_FIRMWARE += "linux-firmware-ath6k firmware-imx-epdc"
