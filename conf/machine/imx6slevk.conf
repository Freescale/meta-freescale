#@TYPE: Machine
#@NAME: NXP i.MX6SL Evaluation Kit
#@SOC: i.MX6SL
#@DESCRIPTION: Machine configuration for NXP i.MX6SL Evaluation Kit
#@MAINTAINER: Otavio Salvador <otavio@ossystems.com.br>

MACHINEOVERRIDES =. "mx6:mx6sl:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc

KERNEL_DEVICETREE = "imx6sl-evk.dtb imx6sl-evk-csi.dtb imx6sl-evk-ldo.dtb \
                     imx6sl-evk-uart.dtb imx6sl-evk-btwifi.dtb"
KERNEL_DEVICETREE_use-mainline-bsp = "imx6sl-evk.dtb"

UBOOT_CONFIG ??= "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'sd-optee', 'sd', d)}"
UBOOT_CONFIG[sd] = "mx6slevk_config,sdcard"
UBOOT_CONFIG[epdc] = "mx6slevk_epdc_config"
UBOOT_CONFIG[spinor] = "mx6slevk_spinor_config"
UBOOT_CONFIG[mfgtool] = "mx6slevk_config"
UBOOT_CONFIG[sd-optee] = "mx6slevk_optee_config,sdcard"

SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_FEATURES += "pci wifi bluetooth"
MACHINE_FEATURES_append_use-fsl-bsp = " optee"

MACHINE_FIRMWARE += "linux-firmware-ath6k firmware-imx-epdc"
