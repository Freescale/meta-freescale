#@TYPE: Machine
#@NAME: NXP i.MX6QP/Q/DL SABRE Smart Device
#@SOC: i.MX6QP/Q/DL
#@DESCRIPTION: Machine configuration for NXP i.MX6QP/Q/DL SABRE Smart Device
#@MAINTAINER: Otavio Salvador <otavio@ossystems.com.br>
#
# This machine replaces the following machines:
#    * imx6qpsabresd
#    * imx6qsabresd
#    * imx6dlsabresd
#    * imx6solosabresd
# and is able to work for all the machines.

MACHINEOVERRIDES =. "mx6:mx6q:mx6dl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa9.inc

KERNEL_DEVICETREE = " \
	imx6qp-sabresd-btwifi.dtb \
	imx6qp-sabresd.dtb \
	imx6qp-sabresd-hdcp.dtb \
	imx6qp-sabresd-ldo.dtb \
	\
	imx6q-sabresd-btwifi.dtb \
	imx6q-sabresd.dtb \
	imx6q-sabresd-enetirq.dtb \
	imx6q-sabresd-hdcp.dtb \
	imx6q-sabresd-ldo.dtb \
	imx6q-sabresd-uart.dtb \
	\
	imx6dl-sabresd-btwifi.dtb \
	imx6dl-sabresd.dtb \
	imx6dl-sabresd-enetirq.dtb \
	imx6dl-sabresd-hdcp.dtb \
	imx6dl-sabresd-ldo.dtb \
"
KERNEL_DEVICETREE_use-mainline-bsp = " \
    imx6qp-sabresd.dtb \
    imx6q-sabresd.dtb \
    imx6dl-sabresd.dtb \
"

WKS_FILE = "imx-uboot-spl-bootpart.wks.in"

### u-boot-fslc settings ###

SPL_BINARY_pn-u-boot-fslc = "SPL"
UBOOT_MACHINE_pn-u-boot-fslc ?= "mx6sabresd_defconfig"
UBOOT_SUFFIX_pn-u-boot-fslc = "img"

### u-boot-imx settings ###

# The u-boot-imx does not provide unified functionality for DL/Q/QP SoC
# variants. Change the defconfig to the targeted SoC variant.
SPL_BINARY_pn-u-boot-imx = ""
UBOOT_MACHINE_pn-u-boot-imx = "mx6qsabresd_defconfig"
UBOOT_MAKE_TARGET_pn-u-boot-imx = "u-boot.imx"
UBOOT_SUFFIX_pn-u-boot-imx = "imx"

WKS_FILE = " \
    ${@bb.utils.contains('IMX_DEFAULT_BOOTLOADER', \
                         'u-boot-fslc', 'imx-uboot-spl-bootpart.wks.in', \
                                        'imx-uboot-bootpart.wks.in', d)}"

SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_FIRMWARE_append_mx6 = " linux-firmware-ath6k"

MACHINE_FEATURES += " pci wifi bluetooth"
