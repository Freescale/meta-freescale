#@TYPE: Machine
#@NAME: NXP i.MX6QP/Q/DL SABRE Smart Device
#@SOC: i.MX6QP/Q/DL
#@DESCRIPTION: Machine configuration for NXP i.MX6QP/Q/DL SABRE Smart Device
#@MAINTAINER: Otavio Salvador <otavio@ossystems.com.br>
#
# This machine replaces the following machines:
#    * imx6qpsabresd
#    * imx6qsabresd
#    * imx6dlsabresd
#    * imx6solosabresd
# and is able to work for all the machines.

MACHINEOVERRIDES =. "mx6:mx6q:mx6dl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa9.inc

KERNEL_DEVICETREE = " \
	imx6qp-sabresd-btwifi.dtb \
	imx6qp-sabresd.dtb \
	imx6qp-sabresd-hdcp.dtb \
	imx6qp-sabresd-ldo.dtb \
	\
	imx6q-sabresd-btwifi.dtb \
	imx6q-sabresd.dtb \
	imx6q-sabresd-enetirq.dtb \
	imx6q-sabresd-hdcp.dtb \
	imx6q-sabresd-ldo.dtb \
	imx6q-sabresd-uart.dtb \
	\
	imx6dl-sabresd-btwifi.dtb \
	imx6dl-sabresd.dtb \
	imx6dl-sabresd-enetirq.dtb \
	imx6dl-sabresd-hdcp.dtb \
	imx6dl-sabresd-ldo.dtb \
"
KERNEL_DEVICETREE_use-mainline-bsp = " \
    imx6qp-sabresd.dtb \
    imx6q-sabresd.dtb \
    imx6dl-sabresd.dtb \
"

UBOOT_MACHINE ?= "mx6sabresd_defconfig"

# These u-boot variables default to the correct settings for u-boot-imx.
# Set overrides for u-boot-fslc.
UBOOT_MAKE_TARGET_pn-u-boot-fslc = "all"
UBOOT_SUFFIX_pn-u-boot-fslc = "img"
SPL_BINARY_pn-u-boot-fslc = "SPL"
WKS_FILE_pn-u-boot-fslc = "imx-uboot-spl-bootpart.wks.in"

# The fsl distro uses u-boot-imx which does not provide unified functionality
# for dl/q/qp SoC variants. Change the defconfig to the targeted SoC variant.
UBOOT_MACHINE_fsl = "mx6qsabresd_defconfig"

SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_FIRMWARE_append_mx6 = " linux-firmware-ath6k"

MACHINE_FEATURES += " pci wifi bluetooth"
