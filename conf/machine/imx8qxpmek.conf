#@TYPE: Machine
#@NAME: i.MX 8QXP MEK
#@DESCRIPTION: i.MX 8QXP Multisensory Enablement Kit (MEK) board
#@MAINTAINER: Lauren Post <lauren.post@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8x:mx8qxp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa35.inc

IMX_DEFAULT_BSP = "nxp"

MACHINE_FEATURES += "pci optee bcm43455 bcm4356"
MACHINE_FEATURES_append_use-nxp-bsp = " bcm4359"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyAMA0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = " \
	freescale/imx8qxp-mek.dtb \
	freescale/imx8qxp-mek-a0.dtb \
	freescale/imx8qxp-mek-dom0.dtb \
	freescale/imx8qxp-mek-dpu-lcdif.dtb \
	freescale/imx8qxp-mek-dpu-lcdif-rpmsg.dtb \
	freescale/imx8qxp-mek-dsi-rm67191.dtb \
	freescale/imx8qxp-mek-dsi-rm67191-rpmsg.dtb \
	freescale/imx8qxp-mek-dsp.dtb \
	freescale/imx8qxp-mek-enet2-tja1100.dtb \
	freescale/imx8qxp-mek-enet2.dtb \
	freescale/imx8qxp-mek-inmate.dtb \
	freescale/imx8qxp-mek-it6263-lvds0-dual-channel.dtb \
	freescale/imx8qxp-mek-it6263-lvds0-dual-channel-rpmsg.dtb \
	freescale/imx8qxp-mek-it6263-lvds1-dual-channel.dtb \
	freescale/imx8qxp-mek-it6263-lvds1-dual-channel-rpmsg.dtb \
	freescale/imx8qxp-mek-jdi-wuxga-lvds0-panel.dtb \
	freescale/imx8qxp-mek-jdi-wuxga-lvds0-panel-rpmsg.dtb \
	freescale/imx8qxp-mek-jdi-wuxga-lvds1-panel.dtb \
	freescale/imx8qxp-mek-jdi-wuxga-lvds1-panel-rpmsg.dtb \
	freescale/imx8qxp-mek-lcdif.dtb \
	freescale/imx8qxp-mek-lcdif-rpmsg.dtb \
	freescale/imx8qxp-mek-ov5640.dtb \
	freescale/imx8qxp-mek-ov5640-rpmsg.dtb \
	freescale/imx8qxp-mek-pcie-ep.dtb \
	freescale/imx8qxp-mek-root.dtb \
	freescale/imx8qxp-mek-rpmsg.dtb \
	freescale/imx8qxp-mek-sof-cs42888.dtb \
	freescale/imx8qxp-mek-sof-wm8960.dtb \
"

UBOOT_MAKE_TARGET = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', '', \
                                                       'u-boot.bin', d)}"
SPL_BINARY = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'spl/u-boot-spl.bin', \
                                                       '', d)}"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx8qxp_mek_defconfig,sdcard"
UBOOT_CONFIG[fspi] = "imx8qxp_mek_fspi_defconfig"

IMX_BOOT_SEEK = "32"

# Set ATF platform name
ATF_PLATFORM = "imx8qx"

IMXBOOT_TARGETS_SD = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl', \
                                                       'flash', d)}"
IMXBOOT_TARGETS_FSPI = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl_flexspi', \
                                                       'flash_flexspi', d)}"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG',        'sd', '${IMXBOOT_TARGETS_SD}', \
                                                       '${IMXBOOT_TARGETS_FSPI}', d)}"
IMX_BOOT_SOC_TARGET = "iMX8QX"

BOARD_TYPE = "mek"

# Add additional firmware
MACHINE_FIRMWARE_append = " linux-firmware-ath10k"
