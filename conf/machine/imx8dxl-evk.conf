#@TYPE: Machine
#@NAME: i.MX 8DXL EVK
#@DESCRIPTION: i.MX 8DXLite Evaluation Kit (EVK) board
#@MAINTAINER: Lauren Post <lauren.post@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8x:mx8dxl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa35.inc

MACHINE_FEATURES += "pci bcm43455 bcm4356"
MACHINE_FEATURES_append_use-nxp-bsp = " bcm4359"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# Disable the serial console due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/imx8dxl-evk.dtb"
KERNEL_DEVICETREE_append_use-nxp-bsp = " \
    freescale/imx8dxl-evk-enet0.dtb \
    freescale/imx8dxl-evk-enet0-tja1100.dtb \
    freescale/imx8dxl-evk-inmate.dtb \
    freescale/imx8dxl-evk-lpspi-slave.dtb \
    freescale/imx8dxl-evk-pcie-ep.dtb \
    freescale/imx8dxl-evk-root.dtb \
    freescale/imx8dxl-evk-rpmsg.dtb \
"

ATF_PLATFORM = "imx8dxl"
IMX_BOOT_SOC_TARGET = "iMX8DXL"

# This machine is not supported by u-boot-fslc, so we force it to use
# u-boot-imx here.
IMX_DEFAULT_BOOTLOADER = "u-boot-imx"

UBOOT_MAKE_TARGET = "all"
SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_SUFFIX = "bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx8dxl_evk_defconfig,sdcard"
UBOOT_CONFIG[fspi] = "imx8dxl_evk_fspi_defconfig"

IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS_SD = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl', \
                                                       'flash', d)}"
IMXBOOT_TARGETS_FSPI = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl_flexspi', \
                                                       'flash_flexspi', d)}"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG',        'sd', '${IMXBOOT_TARGETS_SD}', \
                                                       '${IMXBOOT_TARGETS_FSPI}', d)}"

BOARD_TYPE = "evk"
