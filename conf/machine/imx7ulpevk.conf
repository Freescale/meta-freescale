#@TYPE: Machine
#@NAME: NXP i.MX7ULP Evaluation Kit
#@SOC: i.MX7ULP
#@DESCRIPTION: Machine configuration for NXP i.MX7ULP EVK
#@MAINTAINER: Alexandru Palalau <ioan-alexandru.palalau@nxp.com>

MACHINEOVERRIDES =. "mx7:mx7ulp:"

require conf/machine/include/imx-base.inc
include conf/machine/include/arm/armv7a/tune-cortexa7.inc

MACHINE_FEATURES += "pci wifi bluetooth bcm43430"

KERNEL_DEVICETREE = " \
	imx7ulp-evk.dtb \
"
KERNEL_DEVICETREE:append:use-nxp-bsp = " \
	imx7ulp-evk-ft5416.dtb \
	imx7ulp-evk-mipi.dtb \
	imx7ulp-evkb.dtb \
	imx7ulp-evkb-emmc.dtb \
	imx7ulp-evkb-lpuart.dtb \
	imx7ulp-evkb-mipi.dtb \
	imx7ulp-evkb-rm68191-qhd.dtb \
	imx7ulp-evkb-rm68200-wxga.dtb \
	imx7ulp-evkb-sd1.dtb \
	imx7ulp-evkb-sensors-to-i2c5.dtb \
	imx7ulp-evkb-spi-slave.dtb \
"

UBOOT_MAKE_TARGET = "u-boot.imx"
UBOOT_SUFFIX = "imx"

UBOOT_CONFIG ??= " \
    sd \
    ${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'sd-optee', '', d)} \
"
UBOOT_CONFIG[sd]       = "mx7ulp_evk_config,sdcard"
UBOOT_CONFIG[sd-optee] = "mx7ulp_evk_optee_config,sdcard"
UBOOT_CONFIG[emmc]     = "mx7ulp_evk_emmc_config,sdcard"
UBOOT_CONFIG[mfgtool]  = "mx7ulp_evk_config"

OPTEE_BIN_EXT = "7ulp"

# Set Serial console
SERIAL_CONSOLES = "115200;ttyLP0"
