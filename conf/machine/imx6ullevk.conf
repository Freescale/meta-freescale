#@TYPE: Machine
#@NAME: NXP i.MX6ULL Evaluation Kit
#@SOC: i.MX6ULL
#@DESCRIPTION: Machine configuration for NXP i.MX6ULL EVK
#@MAINTAINER: Lauren Post <lauren.post@nxp.com>

MACHINEOVERRIDES =. "mx6ull:"

include conf/machine/include/imx-base.inc
include conf/machine/include/arm/armv7a/tune-cortexa7.inc

MACHINE_FEATURES += "wifi bluetooth bcm4339 bcm43455 nxp8801-sdio nxp8987-sdio"

KERNEL_DEVICETREE = " \
    nxp/imx/imx6ull-14x14-evk.dtb \
"

KERNEL_DEVICETREE:append:use-nxp-bsp = " \
    nxp/imx/imx6ull-14x14-evk-btwifi.dtb \
    nxp/imx/imx6ull-14x14-evk-emmc.dtb \
    nxp/imx/imx6ull-14x14-evk-gpmi-weim.dtb \
    nxp/imx/imx6ull-14x14-evk-reve.dtb \
    nxp/imx/imx6ull-14x14-evk-reve-btwifi.dtb \
    nxp/imx/imx6ull-14x14-evk-reve-emmc.dtb \
    nxp/imx/imx6ull-14x14-evk-reve-gpmi-weim.dtb \
"

UBOOT_MAKE_TARGET = "u-boot.imx"
UBOOT_SUFFIX = "imx"

UBOOT_CONFIG ??= " \
    sd \
    ${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'sd-optee', '', d)} \
"
UBOOT_CONFIG[sd]       = "mx6ull_14x14_evk_config,sdcard"
UBOOT_CONFIG[sd-optee] = "mx6ull_14x14_evk_optee_config,sdcard"
UBOOT_CONFIG[emmc]     = "mx6ull_14x14_evk_emmc_config,sdcard"
UBOOT_CONFIG[nand]     = "mx6ull_14x14_evk_nand_config,ubifs"
UBOOT_CONFIG[qspi1]    = "mx6ull_14x14_evk_qspi1_config"

OPTEE_BIN_EXT = "6ullevk"
