#@TYPE: Machine
#@NAME: NXP i.MX8MP Evaluation Kit and i.MX8MP Evaluation Kit
#@SOC: i.MX8MP
#@DESCRIPTION: Machine configuration for NXP i.MX8MP EVK
#@MAINTAINER: Alexandru Palalau <ioan-alexandru.palalau@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

MACHINE_FEATURES += "pci wifi bluetooth optee"
MACHINE_FEATURES_use-nxp-bsp_append = " mrvl8997"

# Mainline kernel contains only one DTB file for
# imx8mpevk machine
KERNEL_DEVICETREE = "freescale/imx8mp-evk.dtb"

# NXP kernel has additional DTB files for various board configuration and
# derivates. Include them here for NXP BSP only
KERNEL_DEVICETREE_use-nxp-bsp_append = " \
	freescale/imx8mp-ab2.dtb \
	freescale/imx8mp-evk-basler.dtb \
	freescale/imx8mp-evk-dsp.dtb \
	freescale/imx8mp-evk-flexcan2.dtb \
	freescale/imx8mp-evk-inmate.dtb \
	freescale/imx8mp-evk-jdi-wuxga-lvds-panel.dtb \
	freescale/imx8mp-evk-it6263-lvds-dual-channel.dtb \
	freescale/imx8mp-evk-ov2775.dtb \
	freescale/imx8mp-evk-rm67191.dtb \
	freescale/imx8mp-evk-root.dtb \
	freescale/imx8mp-evk-rpmsg.dtb \
	freescale/imx8mp-evk-sof-wm8960.dtb \
"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "imx8mp_evk_defconfig,sdcard"
UBOOT_CONFIG[fspi] = "imx8mp_evk_defconfig"
UBOOT_CONFIG[ecc] = "imx8mp_evk_inline_ecc_defconfig"
UBOOT_CONFIG[mfgtool] = "imx8mp_evk_defconfig"
SPL_BINARY = "spl/u-boot-spl.bin"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
	lpddr4_pmu_train_1d_dmem_201904.bin \
	lpddr4_pmu_train_1d_imem_201904.bin \
	lpddr4_pmu_train_2d_dmem_201904.bin \
	lpddr4_pmu_train_2d_imem_201904.bin \
"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mp-evk.dtb"

# Set ATF platform name
ATF_PLATFORM = "imx8mp"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_evk_flexspi', 'flash_evk', d)}"
IMX_BOOT_SOC_TARGET = "iMX8MP"

# Set Serial console
SERIAL_CONSOLES = "115200;ttymxc1"

IMAGE_BOOTLOADER = "imx-boot"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""
IMX_BOOT_SEEK = "32"

OPTEE_BIN_EXT = "8mp"

# Add additional firmware
MACHINE_FIRMWARE_append = " linux-firmware-ath10k"
